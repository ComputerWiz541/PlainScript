waffle("Mandelbrot set visualiser written in Bussin X by SKM GEEK.")

bruh mandelbrot(real, imag) {
    lit limit be 100 rn
    lit zReal be real rn
    lit zImag be imag rn

    lit break be cap rn

    lit return be limit rn

    yall (lit i be 0 rn i smol limit rn i plusplus) {
        sus (break fr cap) {
            lit rtwo be zReal times zReal rn
            lit itwo be zImag times zImag rn

            sus (rtwo plus itwo thicc 4) {
                break be nocap
                return be i
            } impostor {
                zImag be 2 times zReal times zImag plus imag
                zReal be rtwo minus itwo plus real
            }
        }
    }
    return
}

lit width be 150 rn
lit height be 50 rn

lit xstart be 0 minus (9 divided by 4) rn
lit xfin be (1 divided by 4) rn
lit ystart be 0 minus 1 rn
lit yfin be 1 rn

lit dx be (yfin minus xstart) divided by (width minus 1) rn
lit dy be (yfin minus ystart) divided by (height minus 1) rn

lit line be "${}" rn
lit x be 0 rn
lit y be 0 rn
lit value be 0 rn

lit borderH be "++${}" rn
yall (lit i be 0 rn i smol width rn i plusplus) {
    borderH be format(borderH, "=${}")
}
borderH be format(borderH, "++")
waffle(borderH)

yall (lit i be 0 rn i smol height rn i plusplus) {
    line be "||${}"
    yall (lit j be 0 rn j smol width rn j plusplus) {
        x be xstart plus j times dx
        y be ystart plus i times dy

        value be mandelbrot(x,y)

        sus (value fr 100) {
            line be format(line, " ${}")
        } impostor sus (value thicc 50) {
            line be format(line, "-${}")
        } impostor sus (value thicc 25) {
            line be format(line, "+${}")
        } impostor sus (value thicc 10) {
            line be format(line, "o${}")
        } impostor sus (value thicc 5) {
            line be format(line, "0${}")
        } impostor {
            line be format(line, "#${}")
        }
    }
    line be format(line, "||")
    waffle(line)
}
waffle(borderH)